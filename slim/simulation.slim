initialize() {
	if (exists("slimgui"))
	{
		setSeed(2067579950);  
		defineConstant("L",  2879248290);
		defineConstant("N",  c(124, 200, 200, 200,  23, 177, 200, 100) );
		defineConstant("tc", c(50,  100, 150, 200, 250, 300, 350) );
		defineConstant("ts", c(16,  95, 109, 192, 254, 281, 314, 322, 329, 353, 378, 400) );
		defineConstant("ss", c( 1,   1,   1,   1,   1,   1,   1,   2,   2,   1,   1,  4) );
		defineConstant("np", 8);
		defineConstant("na", 11);
		defineConstant("i",  1);
		defineConstant("batch_ID", 1);
		defineConstant("project","test");
		defineConstant("ends", c(249218991,249218992,492309988,492309989,690184516,690184517,881213161,
		                         881213162,1061928971,1061928972,1232980287,1232980288,1392107061,
		                         1392107062,1538408217,1538408218,1679518196,1679518197,1815021889,
		                         1815021890,1949967411,1949967412,2083746207,2083746208,2198853203,
		                         2198853204,2306141580,2306141581,2408647688,2408647689,2498811276,
		                         2498811277,2579856545,2579856546,2657871725,2657871726,2716968885,
		                         2716968886,2779918330,2779918331,2828018485,2828018486,2879248290,
		                         2879248291));
    defineConstant("rates", c(1.14856E-08, 0.5,1.10543E-08, 0.5,1.12796E-08, 0.5,1.12312E-08, 0.5,
                              1.12809E-08, 0.5,1.12229E-08, 0.5,1.17646E-08, 0.5,1.14785E-08, 0.5,
                              1.17807E-08, 0.5,1.33651E-08, 0.5,1.17193E-08, 0.5,1.30502E-08, 0.5,
                              1.09149E-08, 0.5,1.11973E-08, 0.5,1.38358E-08, 0.5,1.48346E-08, 0.5,
                              1.58249E-08, 0.5,1.5076E-08, 0.5,1.82201E-08, 0.5,1.71783E-08, 0.5,
                              1.30452E-08, 0.5,1.4445E-08, 0.5));
	}
	initializeSLiMOptions(keepPedigrees=T);
	initializeMutationRate(0.0);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, L);
	initializeRecombinationRate(rates, ends);
	initializeTreeSeq();
}
1 early() {
	
	sim.addSubpop("p1", N[0]);
	
	// schedule SAMPLING BLOCK
	sim.setValue("count_sampling", 0);
	sim.rescheduleScriptBlock(s1, start=ts[0], end=ts[0]);
	
	// schedule DEMOGRAPHY BLOCK
	sim.setValue("count_demography", 0);
	sim.rescheduleScriptBlock(s2, start=tc[0], end=tc[0]);
	
	// schedule CALCULATE Ne BLOCK
	sim.setValue("sum_pIBD", rep(0.0,np));
	sim.rescheduleScriptBlock(s3, start=1, end=ts[na]);
	
	// schedule FINISH BLOCK
	sim.rescheduleScriptBlock(s4, start=ts[na], end=ts[na]);
}

// SAMPLING BLOCK
s1 1000 late() {
	count = sim.getValue("count_sampling");
	sim.treeSeqRememberIndividuals(p1.sampleIndividuals(ss[count]));
	if (count<(na-1)){
		sim.rescheduleScriptBlock(s1, start=ts[count+1], end=ts[count+1]);
	}
	sim.setValue("count_sampling", count+1);

}

// DEMOGRAPHY BLOCK
s2 1000 early(){
	count = sim.getValue("count_demography");
	p1.setSubpopulationSize(N[count+1]);
	if (count<(np-2)){
		sim.rescheduleScriptBlock(s2, start=tc[count+1], end=tc[count+1]);
	}
	sim.setValue("count_demography", count+1);

}

// CALCULATE Ne BLOCK
s3 1000 late() {
   count = sim.getValue("count_demography");	
	parents = unique(sim.subpopulations.individuals.pedigreeParentIDs);
	k = sapply(parents, "sum(sim.subpopulations.individuals.pedigreeParentIDs == applyValue);");
	prob = k/p1.individualCount;
	pIBD = sum(prob*prob*0.25);
	//Ne=1/pIBD;
	//Ne = (2*p1.individualCount-1)/(1+var(k)/2);
	sum_pIBD = sim.getValue("sum_pIBD") ;
	sum_pIBD[count] = pIBD + sum_pIBD[count];
	sim.setValue("sum_pIBD", sum_pIBD );
}

// FINISH BLOCK
s4 1000 late() {
	sim.treeSeqOutput(path="results/"+project+"/"+batch_ID+"/slim"+i+".tree");
	Ne = 2*(ts[na]/np)/sim.getValue("sum_pIBD");
	writeFile(filePath="results/"+project+"/"+batch_ID+"/Ne.txt", contents=paste(i+" "+paste(Ne)	), append=T);
	sim.simulationFinished();
}
